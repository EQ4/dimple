
import sys, os

# Read environment variables & arguments
env = Environment(ENV = os.environ)

# Command-line takes precedence, but environment can specify debug as well
try:
    if ARGUMENTS.has_key('debug'):
        debug = int(ARGUMENTS.get('debug',0))==1
    else:
        debug = int(env['ENV'].get('DIMPLE_DEBUG',0))==1
except:
    debug = False

# Start configuration
conf = Configure(env)

# Check for GL headers
if conf.CheckHeader('/System/Library/Frameworks/OpenGL.framework/Headers/gl.h'):
    env.Append(CPPPATH=['../libdeps/chai3d/darwin/GL'],
               LINKFLAGS=['-framework','OpenGL'])
else:
	conf.CheckHeader('/usr/include/GL/gl.h')

# Check for GLUT headers
if conf.CheckHeader('/System/Library/Frameworks/GLUT.framework/Headers/glut.h'):
    env.Append(CPPPATH=['../libdeps/chai3d/darwin/GLUT'],
               LINKFLAGS=['-framework','GLUT'])
else:
    conf.CheckHeader('/usr/include/GL/glut.h')

    # Using FreeGLUT on all platforms except Mac OS X.
    env.Append(CPPDEFINES = ['USE_FREEGLUT'],
               LIBS = ['glut'])

if not conf.CheckLibWithHeader('samplerate', 'samplerate.h', 'C'):
    env.Append(CPPPATH=['../libdeps/libsamplerate-0.1.2/src'],
               LIBPATH=['../libdeps/libsamplerate-0.1.2/src/.libs'])
    if not conf.CheckLibWithHeader('samplerate', 'samplerate.h', 'C'):
        print 'libsamplerate not found.'
        env.Exit(-1)

# Platform dependant library names
if 'linux' in sys.platform:
	env.Append(LIBS = ['chai3d_linux', 'dhd', 'usb', 'pciscan'],
               LIBPATH = ['../libdeps/chai3d/lib/linux'])
elif 'darwin' in sys.platform:
	env.Append(LIBS = ['chai3d_darwin'],
               LIBPATH = ['../libdeps/chai3d/lib/darwin'])

nv = conf.Finish()

# Debug mode
if debug:
	env.Append(CPPFLAGS = ['-ggdb','-Wall'])
else:
	env.Append(CPPFLAGS = ['-O3'])

env.Append(
    CPPPATH = ['../libdeps/ode-0.7/include',
               '../libdeps/chai3d/include',
               '../libdeps/liblo-0.23'],
    CPPDEFINES = ['_POSIX',
                  'LINUX', '_LINUX',
                  ('_MAX_PATH','260')],
    LIBPATH = ['../libdeps/ode-0.7/ode/src'],
    LIBS = ['ode', 'pthread'],
    LINKFLAGS = ['../libdeps/liblo-0.23/src/.libs/liblo.a']
    )

env.Program( target = 'dimple',
    source = ['CODEMesh.cpp',
              'CODEPotentialProxy.cpp',
              'CODEPrimitive.cpp',
              'CODEPrism.cpp',
              'CODESphere.cpp',
              'OscObject.cpp',
              'valuetimer.cpp',
              'AudioStreamer.cpp',
              'dimple.cpp']
	)
